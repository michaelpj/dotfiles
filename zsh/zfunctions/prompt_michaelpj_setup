# Load dependencies.
pmodload 'helper'

function prompt_michaelpj_git_info {
  if (( _prompt_michaelpj_precmd_async_pid > 0 )); then 
    # Append Git status.
    if [[ -s "$_prompt_michaelpj_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_michaelpj_precmd_async_data"
      RPROMPT+='${git_info:+${(e)git_info[main]} | }${git_info[states]}'
      unalias typeset
    fi

    # Reset PID.
    _prompt_michaelpj_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_michaelpj_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_michaelpj_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_michaelpj_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  RPROMPT=''

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_michaelpj_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_michaelpj_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_michaelpj_git_info WINCH
  prompt_michaelpj_precmd_async &!
  _prompt_michaelpj_precmd_async_pid=$!
}

function prompt_michaelpj_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_michaelpj_precmd_async_pid=0
  _prompt_michaelpj_precmd_async_data="${TMPPREFIX}-prompt_michaelpj_data"

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_michaelpj_precmd

  # Set editor-info parameters.  
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%(?.%F{yellow}.%F{red})❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%(?.%F{yellow}.%F{red})❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info:action' format ': %B%F{yellow}%s%f%b'
  zstyle ':prezto:module:git:info:ahead' format ' %B%F{yellow}ahead%f%b'
  zstyle ':prezto:module:git:info:behind' format ' %B%F{yellow}behind%f%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'

  zstyle ':prezto:module:git:info:indexed' format ' %F{green}M%f'
  zstyle ':prezto:module:git:info:unindexed' format ' %F{red}M%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{red}??%f'
  zstyle ':prezto:module:git:info:clean' format '%B%F{green}clean%f%b'
  zstyle ':prezto:module:git:info:dirty' format '%B%F{red}dirty%f%b:'

  zstyle ':prezto:module:git:info:keys' format \
    'main' '$(coalesce "%b" "%p" "%c")%s' \
    'states' '%C%D%i%I%u'

  # Define prompts.
  PROMPT='%F{magenta}%n%f@%F{yellow}%m%f:%B%F{green}%~%f%B%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT=''
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_michaelpj_setup "$@"

